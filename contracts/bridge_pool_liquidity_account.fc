#include "common/stdlib.fc";
#include "common/dic.fc";
#include "common/message.fc";
#include "common/utils.fc";
#include "bridge_pool_liquidity_account/op.fc";
#include "bridge_pool_liquidity_account/errors.fc";
#include "bridge_pool_liquidity_account/storage.fc";
#include "bridge_pool_liquidity_account/pool-calls.fc";
#include "bridge_pool_liquidity_account/user-calls.fc";
#include "bridge_pool_liquidity_account/get.fc";
#include "bridge_pool_liquidity_account/getter.fc";

() on_bounce(slice in_msg_body) {
    in_msg_body~skip_bits(32);
    int op = in_msg_body~load_uint(32);
    throw_unless(NOT_SUPPORTED_OP, op == remove_liquidity);
    int query_id = in_msg_body~load_uint(64);
    int amount = in_msg_body~load_coins();
    storage::liquidity += amount;
    save_storage();
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) {
        ;; ignore empty messages
        return ();
    }
    var cs = in_msg_full.begin_parse();
    var flags = cs~load_uint(4);  ;; int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool

    slice sender_address = cs~load_msg_addr();

    load_storage();

    if (flags & 1) {
        on_bounce(in_msg_body);
        return ();
    }

    force_chain(WORKCHAIN, sender_address, WRONG_WORKCHAIN);
    (int op, int query_id) = (in_msg_body~load_uint(32), in_msg_body~load_uint(64));

    if (equal_slices(sender_address, storage::bridge_pool_address)) {
        handle_pool_messages(op, query_id, my_balance, msg_value, in_msg_body);
        return ();
    }
    ;; handle message from user
    if (equal_slices(sender_address, storage::owner)) {
        handle_user_messages(op, query_id, my_balance, msg_value, in_msg_body);
        return ();
    }

    if (handle_getter_messages(op, query_id, sender_address, in_msg_body)) {
        return ();
    }
    throw(WRONG_OP);
}

